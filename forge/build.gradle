plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    forge {}
}

loom {
    forge {
        mixinConfig "boxlib.mixins.json"
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    common(project(path: ':boxlib-command', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-config', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-functions', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-gui', configuration: "namedElements")) { transitive false }
    common(project(path: ":boxlib-http", configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-keybind', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-layouts', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-math', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-prompt', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-scheduling', configuration: "namedElements")) { transitive false }
    common(project(path: ':boxlib-translate', configuration: "namedElements")) { transitive false }

    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
    shadowBundle project(path: ':boxlib-command', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-config', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-functions', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-gui', configuration: "transformProductionForge")
    shadowBundle project(path: ":boxlib-http", configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-keybind', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-layouts', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-math', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-prompt', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-scheduling', configuration: "transformProductionForge")
    shadowBundle project(path: ':boxlib-translate', configuration: "transformProductionForge")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

unifiedPublishing {
    project {
        displayName = "[Forge ${rootProject.minecraft_version}] v$rootProject.mod_version"
        version = "$rootProject.version-forge"
        changelog = "A changelog can be found here: https://github.com/Boxadactle/BoxLib/releases/tag/$rootProject.mod_version"
        releaseType = rootProject.release_type
        gameVersions = Arrays.asList(rootProject.game_versions.split(","))
        gameLoaders = [ "forge" ]

        mainPublication tasks.remapJar

        var cfToken = System.getenv("CURSEFORGE_TOKEN")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = rootProject.project_id_curseforge
            }
        }

        var modrinthToken = System.getenv("MODRINTH_TOKEN")
        if (modrinthToken != null) {
            modrinth {
                token = modrinthToken
                id = rootProject.project_id_modrinth
                version = "${rootProject.mod_version}-${project.name}"
            }
        }
    }
}